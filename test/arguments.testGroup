#!/usr/bin/env rexx

parse source . . s

group = .TestGroup~new(s)
group~add(.arguments.testGroup)

if group~isAutomatedTest then return group

testResult = group~suite~execute~~print

return testResult

::requires 'ooTest.frm'
::requires 'orxpython.cls'

/*
    These tests cover Python's unique feature of positional and keyword function arguments.

    See:
    https://docs.python.org/3/tutorial/controlflow.html#keyword-arguments
    https://docs.python.org/3/tutorial/controlflow.html#special-parameters
*/
::class arguments.testGroup public subclass ooTestCase
    ::method test_open_withKwargs
        /*
            Function parameters:

            open(file, mode='r', buffering=-1, encoding=None, errors=None, newline=None, closefd=True, opener=None)
        */

        -- Write to file.
        kwargs = .Python~kwargs
        kwargs['mode'] = 'w'
        kwargs['encoding'] = 'utf-8'

        f = .Python~open('test-output/test_arguments.txt', kwargs)
        f~write('hello, world')
        f~close

        -- Read from file.
        f = .Python~open('test-output/test_arguments.txt')
        actual = f~read
        f~close
        self~assertSame('hello, world', unbox(actual))

    ::method test_open_withDict
        /*
            Function parameters:

            open(file, mode='r', buffering=-1, encoding=None, errors=None, newline=None, closefd=True, opener=None)
        */

        -- Write to file.
        dict = .Python~dict
        dict['mode'] = 'w'
        dict['encoding'] = 'utf-8'

        kwargs = .Python~kwargs(dict)

        f = .Python~open('test-output/test_arguments.txt', kwargs)
        f~write('hello, world')
        f~close

        -- Read from file.
        f = .Python~open('test-output/test_arguments.txt')
        actual = f~read
        f~close
        self~assertSame('hello, world', unbox(actual))
