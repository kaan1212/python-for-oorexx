#!/usr/bin/env rexx

parse source . . s

group = .TestGroup~new(s)
group~add(.csv.testGroup)

if group~isAutomatedTest then return group

testResult = group~suite~execute~~print

return testResult

::requires 'ooTest.frm'
::requires 'PyRexx.cls'

/*
    See: https://docs.python.org/3/library/csv.html
*/
::class csv.testGroup public subclass ooTestCase
    ::method setUp
        expose py
        py = .PyRexx~new

    ::method test_writer_reader
        /*
            Python example:
            import csv

            file = 'some.csv'

            with open(file, 'w', newline='') as f:
                writer = csv.writer(f)
                writer.writerow(['a', 'b', 'c'])

            with open(file, newline='') as f:
                reader = csv.reader(f)
                for row in reader:
                    print(row)

            Output:
            ['a', 'b', 'c']
        */

        expose py

        py~import('csv')

        file = 'test-output/test_csv.csv'

        kwargs = py~kwargs
        kwargs['newline'] = ''

        -- Write.
        f = py~open(file, 'w', kwargs)
        writer = py~csv.writer(f)
        writer~writerow(py['a', 'b', 'c'])
        f~close

        -- Read.
        f = py~open(file, kwargs)
        reader = py~csv.reader(f)
        row = py~next(reader)
        f~close

        self~assertSame("['a', 'b', 'c']", row)

    ::method test_reader_str
        /*
            Python example:
            import csv
            for row in csv.reader(['one,two,three'])
                print(row)

            Output:
            ['one', 'two', 'three']
        */

        expose py

        py~import('csv')

        csvreader = py~csv.reader(py['one,two,three'])
        row = py~next(csvreader)

        self~assertSame("['one', 'two', 'three']", row)
