#!/usr/bin/env rexx

parse source . . s

group = .TestGroup~new(s)
group~add(.json.testGroup)

if group~isAutomatedTest then return group

testResult = group~suite~execute~~print

return testResult

::requires 'ooTest.frm'
::requires 'PyRexx.cls'

/*
    See: https://docs.python.org/3/library/json.html
*/
::class json.testGroup public subclass ooTestCase
    ::method setUp
        expose py
        py = .PyRexx~new

    ::method test_dumps
        /*
            Python example:
            >>> import json
            >>> json.dumps(['foo', {'bar': ('baz', None, 1.0, 2)}])
            '["foo", {"bar": ["baz", null, 1.0, 2]}]'
        */

        expose py

        py~import('json')

        objChild = py~dict
        objChild['bar'] = py~tuple(py['baz', py~None, py~float(1.0), py~int(2)])
        obj = py['foo', objChild]
        actual = py~dumps(obj)

        self~assertSame('["foo", {"bar": ["baz", null, 1.0, 2]}]', actual)

    ::method test_dumps_sortKeys
        /*
            Python example:
            >>> import json
            >>> print(json.dumps({"c": 0, "b": 0, "a": 0}, sort_keys=True))
            {"a": 0, "b": 0, "c": 0}
        */

        expose py

        py~import('json')

        dict = py~dict
        dict['c'] = py~int(0)
        dict['b'] = py~int(0)
        dict['a'] = py~int(0)

        kwargs = py~kwargs
        kwargs['sort_keys'] = py~True
        actual = py~dumps(dict, kwargs)

        self~assertSame('{"a": 0, "b": 0, "c": 0}', actual)

    ::method test_dumps_compactEncoding
        /*
            Python example:
            >>> import json
            >>> json.dumps([1, 2, 3, {'4': 5, '6': 7}], separators=(',', ':'))
            '[1,2,3,{"4":5,"6":7}]'
        */

        expose py

        py~import('json')

        dict = py~dict
        dict['4'] = py~int(5)
        dict['6'] = py~int(7)
        obj = py[py~int(1), py~int(2), py~int(3), dict]

        kwargs = py~kwargs
        kwargs['separators'] = py~tuple(py[',', ':'])
        actual = py~dumps(obj, kwargs)

        self~assertSame('[1,2,3,{"4":5,"6":7}]', actual)

    ::method test_dumps_prettyPrinting
        /*
            Python example:
            >>> import json
            >>> print(json.dumps({'6': 7, '4': 5}, sort_keys=True, indent=4))
            {
                "4": 5,
                "6": 7
            }
        */

        expose py

        py~import('json')

        dict = py~dict
        dict['6'] = py~int(7)
        dict['4'] = py~int(5)

        kwargs = py~kwargs
        kwargs['sort_keys'] = py~True
        kwargs['indent'] = py~int(4)
        actual = py~dumps(dict, kwargs)

        nl = .String~nl
        expected = -
            '{'nl || -
            '    "4": 5,'nl || -
            '    "6": 7'nl || -
            '}'

        self~assertSame(expected, actual)

    ::method test_loads
        /*
            Python example:
            >>> import json
            >>> json.loads('["foo", {"bar":["baz", null, 1.0, 2]}]')
            ['foo', {'bar': ['baz', None, 1.0, 2]}]
        */

        expose py

        py~import('json')
        actual = py~loads('["foo", {"bar":["baz", null, 1.0, 2]}]')

        expectedObjChild = py~dict
        expectedObjChild['bar'] = py['baz', py~None, py~float(1.0), py~int(2)]
        expectedObj = py['foo', expectedObjChild]

        self~assertSame(expectedObj~makeString, actual~makeString)
